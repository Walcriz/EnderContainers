plugins {
    id 'java'
    id 'maven-publish'
    id 'jacoco'
}

group = rootProject.group
version = rootProject.version

compileJava {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
    options.encoding = 'UTF-8'
}

jar {
    archiveBaseName.set("endercontainers-${project.name}")
    from "${rootDir}/LICENSE"
}

configurations {
    testImplementation.extendsFrom compileOnly
}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.20-R0.1-SNAPSHOT'

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:4.11.0'
    testImplementation 'org.assertj:assertj-core:3.24.2'
}

publishing {
    repositories {
        maven {
            name 'Personal'
            if (project.version.endsWith('-SNAPSHOT')) {
                url 'https://repo.utarwyn.fr/snapshots/'
            } else {
                url 'https://repo.utarwyn.fr/releases/'
            }
            credentials {
                username = 'utarwyn'
                password = project.findProperty('personal.token') ?: System.getenv('PERSONAL_TOKEN')
            }
        }
        maven {
            name 'GitHubPackages'
            url 'https://maven.pkg.github.com/utarwyn/EnderContainers'
            credentials {
                username = 'utarwyn'
                password = project.findProperty('github.token') ?: System.getenv('GITHUB_TOKEN')
            }
        }
    }
}

jacocoTestReport {
    reports {
        xml.required.set(true)
    }
}
